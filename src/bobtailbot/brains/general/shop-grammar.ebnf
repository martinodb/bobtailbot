(* EBNF grammar for instaparse *)
<INPUT> =  <';'>* [RULES-QUERIES | NQUERY | FACTS] ;


NQUERY = <'query'> NAME <';'>*;


<RULES-QUERIES> = [DISCOUNT | PROMOTION | QUERY]+ ;
    PROMOTION = <'promotion'> NAME PROMOTIONTYPE <'when'> CONDITION [<'and'> CONDITION]* <';'>+;
    DISCOUNT = <'discount'> NAME PERCENT <'when'> CONDITION [<'and'> CONDITION]* <';'>+;
    QUERY = <'match'> QCONDITION [<'and'> QCONDITION]* <';'>*;
    
    <FACTS> = [F-CUSTOMER | F-ORDER | F-PURCHASE]+ ;
    F-CUSTOMER = <'customer'> [<'status'> <'is'>]? STRING <';'>+;
    F-ORDER = <'order'> [<'year'> <'is'>]? NUMBER <','>? [<'month'> <'is'>]? STRING <','>? [<'day'> <'is'>]? NUMBER <';'>+ ;
    F-PURCHASE = <'purchase'> [<'cost'> <'is'>]? NUMBER <','>? [<'item'> <'is'>]? STRING  <';'>+ ;
    
    <PERCENT> = NUMBER ;
    PROMOTIONTYPE = STRING ;
    
    <NAME> = STRING ;

    NUMBER = #'[0-9]+' ;
    
    (* <STRING> = #'[A-Za-z][A-Za-z0-9_-]+' ; *)
    <STRING> = #'[A-Za-z][A-Za-z0-9\/\._-]+' ;
    
    CONDITION = FACTTYPE FIELD OPERATOR VALUE ;
    QCONDITION = FACTTYPE FIELD OPERATOR VALUE ;
    FACTTYPE = 'customer' | 'total' | 'order' ;
    <FIELD> = STRING ;
    OPERATOR = 'is' | '>' | '<' | '=' ;
    <VALUE> = STRING | NUMBER ;
